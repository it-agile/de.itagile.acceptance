plugins {
    id 'java-library'
}

defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

test {
    exclude '**/RunAllFeatures.class'
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
            	'--tags', 'not @ignore',
            	'--plugin', 'html:build/reports/cucumber/cucumber-html-report.html',
            	'--plugin', 'pretty']
        }
    }
}

dependencies {

	// Selenium
	implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
	
    // Cucumber
  	testImplementation 'io.cucumber:cucumber-java:6.7.0'
  	testImplementation 'io.cucumber:cucumber-junit:6.7.0'
  	testImplementation 'io.cucumber:cucumber-picocontainer:6.7.0'
  	
  	// Hamcrest
  	testImplementation 'org.hamcrest:hamcrest-all:1.3'

}

build {
    dependsOn cucumber
}
